/***********************************************************************
 * @file	rayCasting.comp
 * @author	jjyou
 * @date	2024-4-10
 * @brief	This file implements ray casting algorithm.
***********************************************************************/

#version 450

layout (local_size_x = 32, local_size_y = 32) in;

/** @brief	Input TSDF volume.
  * 
  *			A storage buffer containing all information about the TSDF volume.
  */
layout(set = 0, binding = 0) readonly buffer TSDFVolume {
	uvec3 resolution;
	float size;
	vec3 corner;
	float truncationDistance;
	ivec2 data[];
} tsdfVolume;

/** @brief	Ray casting parameters.
  */
layout(set = 1, binding = 0) uniform RayCastingParameters {
	float fx, fy, cx, cy;
	mat4 invView;
	float minDepth;
	float maxDepth;
	float invalidDepth;
	float marchingStep;
} rayCastingParameters;

/** @brief	Output surface textures.
  * 
  *			Three textures for color, depth, and normal respectively.
  *			The depth map is true-depth. It is not in the clipped space of
  *			graphics rendering.
  */
layout (set = 2, binding = 0, rgba8) uniform image2D surfaceColorTexture;
layout (set = 2, binding = 1, r32f) uniform image2D surfaceDepthTexture;
layout (set = 2, binding = 2, rgba8) uniform image2D surfaceNormalTexture;

#include "tsdfVolumeCommon.h"

#include "rayCastingCommon.h"

void main(){

	ivec2 outputPixelPos = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);
	ivec2 outputSize = imageSize(surfaceColorTexture);
	if (outputPixelPos.x >= outputSize.x || outputPixelPos.y >= outputSize.y)
		return;

	// Compute ray direction and origin in the world space
	vec3 rayOrigin = rayCastingParameters.invView[3].xyz;
	vec3 rayDir = vec3(
		(float(outputPixelPos.x) - rayCastingParameters.cx) / rayCastingParameters.fx,
		(float(outputPixelPos.y) - rayCastingParameters.cy) / rayCastingParameters.fy,
		1.0
	);
	float scaleFactor = length(rayDir);
	rayDir = normalize(mat3(rayCastingParameters.invView) * rayDir);

	// Ray casting
	vec4 outColor;
	float outDepth;
	vec3 outNormal;
	float rayCastingResult = rayCasting(rayOrigin, rayDir, rayCastingParameters.minDepth * scaleFactor, rayCastingParameters.maxDepth * scaleFactor);
	if (isinf(rayCastingResult)) {
		outColor = vec4(0.0, 0.0, 0.0, 0.0);
		outDepth = rayCastingParameters.invalidDepth;
		outNormal = vec3(0.0, 0.0, 0.0);
	} else {
		vec3 pos = rayOrigin + rayCastingResult * rayDir;
		outColor = interpolateColor(pos);
		outDepth = rayCastingResult / scaleFactor;
		outNormal = computeNormal(pos);
	}

	// Store
	imageStore(surfaceColorTexture, outputPixelPos, outColor);
	imageStore(surfaceDepthTexture, outputPixelPos, vec4(outDepth));
	imageStore(surfaceNormalTexture, outputPixelPos, vec4(outNormal * 0.5 + 0.5, 1.0));
}